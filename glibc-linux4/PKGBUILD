# Maintainer: Heptazhou <zhou at 0h7z dot com>

# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>

# toolchain build order: linux-api-headers->glibc->binutils->gcc->glibc->binutils->gcc
# NOTE: valgrind requires rebuilt with each major glibc version

pkgbase=glibc-linux4
pkgname=($pkgbase lib32-$pkgbase)
pkgver_=6d1e3fb07b45e2e31e469b16cf21b24bccf8914c
pkgver=2.39
pkgrel=1
arch=(x86_64)
url="https://www.gnu.org/software/libc/"
license=(GPL-2.0-or-later LGPL-2.1-or-later)
makedepends=(git gd lib32-gcc-libs python)
options=(staticlibs !lto)
validpgpkeys=(
	7273542B39962DF7B299931416792B4EA25340F8 # Carlos O'Donell
	BC7C7372637EC10C57D7AA6579C43DFBF1CF2187 # Siddhesh Poyarekar
)
source=(
	# git+https://sourceware.org/git/glibc.git#commit=$pkgver_
	https://ftp.gnu.org/gnu/glibc/glibc-$pkgver.tar.xz{,.sig}
	$pkgbase.patch
	locale.gen.txt
	locale-gen
	lib32-glibc.conf
	sdt.h
	sdt-config.h
)
sha256sums=(
	# "SKIP"
	"f77bd47cf8170c57365ae7bf86696c118adb3b120d3259c64c502d3dc1e2d926" "SKIP"
	"41ee9b05d2bfa478622b72ca5be553061072e0e52197a8550fae0dd7ee56405c"
	"2a7dd6c906b6c54a68f48a21898664a32fdb136cbd9ff7bfd48f01d1aaa649ae"
	"8c5cc09018cbd65e8570430f872e118caa2644e311d6200b7d5f0cba837fbba4"
	"c27424154a6096ae32c0824b785e05de6acef33d9224fd6147d1936be9b4962b"
	"774061aff612a377714a509918a9e0e0aafce708b87d2d7e06b1bd1f6542fe70"
	"cdc234959c6fdb43f000d3bb7d1080b0103f4080f5e67bcfe8ae1aaf477812f0"
)
# https://gitlab.archlinux.org/archlinux/packaging/packages/glibc/-/commits/2.39-4
# _commit=31da30f23cddd36db29d5b6a1c7619361b271fb4

prepare() {
	mkdir -p glibc-build lib32-glibc-build

	[[ -d glibc-$pkgver ]] && ln -s glibc-$pkgver glibc
	cd glibc

	# compatibility with linux 4.x hosts
	# patch -Np1 < "$srcdir/$pkgbase.patch"
}

build() {
	local _configure_flags=(
		--prefix=/usr
		--with-headers=/usr/include
		--with-bugurl=https://gitlab.archlinux.org/archlinux/packaging/packages/glibc/-/issues
		--enable-bind-now
		--enable-fortify-source
		--enable-kernel=4.4.0
		--enable-multi-arch
		--enable-stack-protector=strong
		--enable-systemtap
		--disable-nscd
		--disable-profile
		--disable-werror
	)

	(
		cd glibc-build

		echo "slibdir=/usr/lib" >> configparms
		echo "rtlddir=/usr/lib" >> configparms
		echo "sbindir=/usr/bin" >> configparms
		echo "rootsbindir=/usr/bin" >> configparms

		# Credits @allanmcrae
		# https://github.com/allanmcrae/toolchain/blob/f18604d70c5933c31b51a320978711e4e6791cf1/glibc/PKGBUILD
		# remove fortify for building libraries
		CFLAGS=${CFLAGS/-Wp,-D_FORTIFY_SOURCE=3/}

		"${srcdir}"/glibc/configure \
			--libdir=/usr/lib \
			--libexecdir=/usr/lib \
			--enable-cet \
			"${_configure_flags[@]}"

		make -O

		# build info pages manually for reproducibility
		make info
	)

	(
		cd lib32-glibc-build
		export CC="gcc -m32 -mstackrealign"
		export CXX="g++ -m32 -mstackrealign"

		echo "slibdir=/usr/lib32" >> configparms
		echo "rtlddir=/usr/lib32" >> configparms
		echo "sbindir=/usr/bin" >> configparms
		echo "rootsbindir=/usr/bin" >> configparms

		"${srcdir}"/glibc/configure \
			--host=i686-pc-linux-gnu \
			--libdir=/usr/lib32 \
			--libexecdir=/usr/lib32 \
			"${_configure_flags[@]}"

		make -O
	)

	# pregenerate locales here instead of in package
	# functions because localedef does not like fakeroot
	make -C "${srcdir}"/glibc/localedata objdir="${srcdir}"/glibc-build \
		DESTDIR="${srcdir}"/locales install-locale-files
}

check() {
	cd glibc-build

	# adjust/remove buildflags that cause false-positive testsuite failures
	sed -i '/FORTIFY/d' configparms # failure to build testsuite
	sed -i 's/-Werror=format-security/-Wformat-security/' config.make # failure to build testsuite
	sed -i '/CFLAGS/s/-fno-plt//' config.make # 16 failures
	sed -i '/CFLAGS/s/-fexceptions//' config.make # 1 failure

	# some failures are "expected"
	# make -O check || \ #
	true
}

package_glibc-linux4() {
	pkgdesc="GNU C Library (linux 4.x compat)"
	depends=("linux-api-headers>=4.10" tzdata filesystem)
	provides=("glibc=$pkgver")
	conflicts=("glibc")
	optdepends=("gd: for memusagestat" "perl: for mtrace")
	install=glibc.install
	backup=(
		etc/gai.conf
		etc/locale.gen
	)

	make -C glibc-build DESTDIR="${pkgdir}" install
	rm -f "${pkgdir}"/etc/ld.so.cache

	# Shipped in tzdata
	rm -f "${pkgdir}"/usr/bin/{tzselect,zdump,zic}

	cd glibc

	install -dm755 "${pkgdir}"/usr/lib/locale

	install -m644 posix/gai.conf "${pkgdir}"/etc/gai.conf

	install -m755 "${srcdir}"/locale-gen "${pkgdir}"/usr/bin

	# Create /etc/locale.gen
	install -m644 "${srcdir}"/locale.gen.txt "${pkgdir}"/etc/locale.gen
	sed -e '1,3d' -e 's|/| |g' -e 's|\\| |g' -e 's|^|#|g' \
		localedata/SUPPORTED >> "${pkgdir}"/etc/locale.gen

	# Add SUPPORTED file to pkg
	sed -e '1,3d' -e 's|/| |g' -e 's| \\||g' \
		localedata/SUPPORTED > "${pkgdir}"/usr/share/i18n/SUPPORTED

	# install C.UTF-8 so that it is always available
	# should be built into glibc eventually
	# https://sourceware.org/glibc/wiki/Proposals/C.UTF-8
	# https://bugs.archlinux.org/task/74864
	install -dm755 "${pkgdir}"/usr/lib/locale
	cp -r "${srcdir}"/locales/usr/lib/locale/C.utf8 -t "${pkgdir}"/usr/lib/locale
	sed -i '/#C\.UTF-8 /d' "${pkgdir}"/etc/locale.gen

	# Provide tracing probes to libstdc++ for exceptions, possibly for other
	# libraries too. Useful for gdb's catch command.
	install -Dm644 "${srcdir}"/sdt.h "${pkgdir}"/usr/include/sys/sdt.h
	install -Dm644 "${srcdir}"/sdt-config.h "${pkgdir}"/usr/include/sys/sdt-config.h
}

package_lib32-glibc-linux4() {
	pkgdesc="GNU C Library (linux 4.x compat, 32-bit)"
	depends=("glibc=$pkgver")
	provides=("lib32-glibc=$pkgver")
	conflicts=("lib32-glibc")
	options+=("!emptydirs")

	cd lib32-glibc-build

	make DESTDIR="${pkgdir}" install
	rm -rf "${pkgdir}"/{etc,sbin,usr/{bin,sbin,share},var}

	# We need to keep 32 bit specific header files
	find "${pkgdir}"/usr/include -type f -not -name '*-32.h' -delete

	# Dynamic linker
	install -d "${pkgdir}"/usr/lib
	ln -s ../lib32/ld-linux.so.2 "${pkgdir}"/usr/lib/

	# Add lib32 paths to the default library search path
	install -Dm644 "${srcdir}"/lib32-glibc.conf "${pkgdir}"/etc/ld.so.conf.d/lib32-glibc.conf

	# Symlink /usr/lib32/locale to /usr/lib/locale
	ln -s ../lib/locale "${pkgdir}"/usr/lib32/locale
}
