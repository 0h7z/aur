# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: morwel
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Stéphane Gaudreault <stephane@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Jason Chu <jason@archlinux.org>

pkgname=python
pkgver=3.13.3
pkgrel=2
_pybasever=${pkgver%.*}
pkgdesc="The Python programming language"
arch=("x86_64")
license=("PSF-2.0")
url="https://www.python.org/"
depends=("bzip2" "expat" "gdbm" "libffi" "libnsl" "libxcrypt" "openssl" "zlib" "tzdata" "mpdecimal")
makedepends=("tk" "sqlite" "bluez-libs" "llvm" "gdb" "xorg-server-xvfb" "ttf-font")
optdepends=(
	"python-setuptools: for building Python packages using tooling that is usually bundled with Python"
	"python-pip: for installing Python packages using tooling that is usually bundled with Python"
	"python-pipx: for installing Python software not packaged on Arch Linux"
	"sqlite: for a default database integration"
	"xz: for lzma"
	"tk: for tkinter"
)
source=(
	https://www.python.org/ftp/python/${pkgver%rc*}/Python-${pkgver}.tar.xz{,.asc}
	EXTERNALLY-MANAGED
)
sha512sums=(
	"f7559b6dceae69f48742af0a6497fbec42cd1e5304f64b6eb9d89222a1171ccf12fa186cc0decabb4e98d05223184967a4a7537754c01083dacdc9073cb1a578" "SKIP"
	"3d0a4723426e81f21fa49d4f9c14e2f73a7473f26feaeaf30ab38b2a31f7c62b327d4c319ed392f0e3105cba33e1083ac7866175ccb51440a015123ea3b10973"
)
validpgpkeys=(
	"0D96DF4D4110E5C43FBFB17F2D347EA6AA65421D" # Ned Deily (Python release signing key) <nad@python.org>
	"E3FF2839C048B25C084DEBE9B26995E310250568" # Łukasz Langa (GPG langa.pl) <lukasz@langa.pl>
	"A035C8C19219BA821ECEA86B64E628F8D684696D" # Pablo Galindo Salgado <pablogsal@gmail.com>
	"7169605F62C751356D054A26A821E680E5FA6305" # Thomas Wouters <thomas@xs4all.nl>
)

prepare() {
	cd Python-${pkgver}

	# Ensure that we are using the system copy of various libraries (expat, libffi, and libmpdec),
	# rather than copies shipped in the tarball
	rm -r Modules/expat
	rm -r Modules/_decimal/libmpdec
}

build() {
	cd Python-${pkgver}

	# PGO should be done with -O3
	CFLAGS="${CFLAGS/-O2/-O3} -ffat-lto-objects"

	# Disable bundled pip & setuptools
	./configure \
		--prefix=/usr \
		--enable-shared \
		--with-computed-gotos \
		--enable-optimizations \
		--with-lto \
		--enable-ipv6 \
		--with-system-expat \
		--with-dbmliborder=gdbm:ndbm \
		--with-system-libmpdec \
		--enable-loadable-sqlite-extensions \
		--without-ensurepip \
		--with-tzpath=/usr/share/zoneinfo

	# Obtain next free server number for xvfb-run; this even works in a chroot environment.
	export servernum=99
	while ! xvfb-run -a -n "$servernum" /bin/true 2> /dev/null; do servernum=$((servernum + 1)); done

	LC_CTYPE=en_US.UTF-8 xvfb-run -s "-screen 0 1920x1080x16 -ac +extension GLX" -a -n "$servernum" make EXTRA_CFLAGS="$CFLAGS"
}

package() {
	cd Python-${pkgver}

	# Hack to avoid building again
	sed -i 's/^all:.*$/all: build_all/' Makefile

	# PGO should be done with -O3
	CFLAGS="${CFLAGS/-O2/-O3}"

	make DESTDIR="${pkgdir}" EXTRA_CFLAGS="$CFLAGS" install

	# Why are these not done by default...
	ln -s python3               "${pkgdir}"/usr/bin/python
	ln -s python3-config        "${pkgdir}"/usr/bin/python-config
	ln -s idle3                 "${pkgdir}"/usr/bin/idle
	ln -s pydoc3                "${pkgdir}"/usr/bin/pydoc
	ln -s python${_pybasever}.1 "${pkgdir}"/usr/share/man/man1/python.1

	# some useful "stuff" FS#46146
	install -dm755 "${pkgdir}"/usr/lib/python${_pybasever}/Tools/{i18n,scripts}
	install -m755 Tools/i18n/{msgfmt,pygettext}.py "${pkgdir}"/usr/lib/python${_pybasever}/Tools/i18n/
	install -m755 Tools/scripts/{README,*py} "${pkgdir}"/usr/lib/python${_pybasever}/Tools/scripts/

	# PEP668
	install -Dm644 "$srcdir"/EXTERNALLY-MANAGED -t "${pkgdir}/usr/lib/python${_pybasever}/"

	# Split tests
	cd "$pkgdir"/usr/lib/python*/
	rm -r {test,idlelib/idle_test}
}
