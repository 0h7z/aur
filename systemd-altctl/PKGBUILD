# Maintainer: Heptazhou <zhou at 0h7z dot com>

pkgname=systemd-altctl
pkgver=1.5.8066
pkgrel=1
pkgdesc="systemctl replacement for system without systemd"
arch=("any")
url="https://github.com/gdraheim/docker-systemctl-replacement"
license=("EUPL-1.2")
depends=("python" "systemd")
options=(!debug)
source=(
	"journalctl.py::$url/raw/v$pkgver/files/docker/journalctl3.py"
	"systemctl.py::$url/raw/v$pkgver/files/docker/systemctl3.py"
	"00-$pkgname-update.hook"
	"00-$pkgname-remove.hook"
)
sha256sums=(
	"ac580249165ceffa1b8f4cbf89fadcf6cd3374c1f4898e8166b152017113033d"
	"f8736b56299374a316a958e5e949be73e657e2f7069691381664d94950c645c9"
	"13a30c36aebb3d4e983f3ade305cb2543471dcb3105c73dccb0f8751ebacf7cf"
	"04445ca4a56cbb1fb2e379e3e4663d31963b508dbfd07c0d56894171e10cedfd"
)
# https://github.com/gdraheim/docker-systemctl-replacement/tags

package() {
	cd -- "$srcdir/"
	install -Dm644 -t "$pkgdir/usr/share/libalpm/hooks/" "00-$pkgname"-*.hook
	install -Dm755 -t "$pkgdir/usr/bin/"       {journal,system}"ctl.py"
}

build() {
	cd -- "$srcdir/"
	sed -r 's/\b(systemctl)3(.py)\b/\1\2/g' -i "journalctl.py"
	sed -r 's/^(#\!.*)\b(python)3\b/\1\2/g' -i {journal,system}"ctl.py"
}
